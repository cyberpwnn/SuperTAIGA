buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
    }
}

def modid = "taiga"
def modname = "SUPER Tinkers Alloying Addition"
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "com.matthewprenger.cursegradle"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }

    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
}

group = "com.sosnitzka" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "taiga"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


task buildInfo {
    if (System.getenv().TAIGA_TAG != null) {
        ext.revision = System.getenv().TAIGA_TAG
    } else {
        ext.revision = "snapshot"
    }

    if (System.getenv().BUILD_NUMBER != null) {
        ext.buildNum = System.getenv().BUILD_NUMBER
    }

    def cmd = "git log \$(git tag --sort=-refname | sed -n '2p')..\$(git tag --sort=-refname | sed -n '1p') --oneline"
    def proc = cmd.execute()
    proc.waitFor()
    if (proc.exitValue() == 0) {
        ext.changes = proc.text.trim()
    } else {
        ext.changes = "N/A"
    }
}

version = project.buildInfo.revision

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20171003"
    replace '@VERSION@', project.version
}

//noinspection GroovyAssignabilityCheck
ext.mc_version = project.minecraft.version.split('-')[0]
version = "${mc_version}-${project.buildInfo.revision}"

dependencies {
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.55"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.12.0.125"
    deobfCompile "mezz.jei:jei_1.12.2:4.+"
}


processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "modid", modid
    inputs.property "modname", modname

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version, 'modid':modid, 'modname':modname
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    //noinspection GroovyAssignabilityCheck
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives deobfJar
}
